apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: 'and_res_guard.gradle'
apply plugin: 'bugly'

def buglyAppId = 'c6e450ad19'
def buglyAppKey = '54ece6d9-fcb0-4ce2-8dae-b86efb65617c'
def buglyAppIdDebug = 'c6e450ad19'
def buglyAppKeyDebug = '54ece6d9-fcb0-4ce2-8dae-b86efb65617c'
def buglyAppIdRelease = 'c6e450ad19'
def buglyAppKeyRelease = '54ece6d9-fcb0-4ce2-8dae-b86efb65617c'

android {
  compileSdkVersion 29
  defaultConfig {
    applicationId rootProject.ext.android.applicationId
    minSdkVersion 23
    targetSdkVersion 29
    versionCode rootProject.ext.android.versionCode
    versionName rootProject.ext.android.versionName
    resValue "string", "build_time", rootProject.ext.android.buildTime
    //允许使用SVG
    //    vectorDrawables.useSupportLibrary = true
    multiDexEnabled true
    resConfigs "zh", "en" //保留中文和英文资源
    ndk {
      abiFilters 'armeabi-v7a' //, 'arm64-v8a'
    }
    //资源文件名冲突，引用包重复，文件名显示的格式与文件本身的格式不对应
    //    aaptOptions.cruncherEnabled = false
    //    aaptOptions.useNewCruncher = false
    //RxHttp需要 https://github.com/liujingxing/okhttp-RxHttp
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [
            ////必须，告知RxHttp你依赖的okhttp版本，目前已适配 v3.12.0 - v4.7.2版本
            rxhttp_okhttp: '4.7.2',
            //使用asXxx方法时必须，告知RxHttp你依赖的rxjava版本，可传入rxjava2、rxjava3
            rxhttp_rxjava: 'rxjava2'
        ]
      }
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  //flavorDimensions "default"
  ////公开发布商店渠道配置
  //productFlavors {
  //  //程序员本地跑的包(只直接在AS中跑，不打包给其他人)
  //  Developer {
  //    dimension "default"
  //  }
  //  //打包后★必须使用多渠道设置渠道★(不直接跑包，打包时使用)
  //  Package {
  //    dimension "default"
  //  }
  //}

  //配置不同版本的keystore
  signingConfigs {
    debug {
      storeFile file(STORE_FILE_ABASE)
      storePassword STORE_PASSWORD_ABASE
      keyAlias KEY_ALIAS_ABASE
      keyPassword KEY_PASSWORD_ABASE
    }
    release {
      storeFile file(STORE_FILE_ABASE)
      storePassword STORE_PASSWORD_ABASE
      keyAlias KEY_ALIAS_ABASE
      keyPassword KEY_PASSWORD_ABASE
    }
  }
  buildTypes {
    debug {
      debuggable true
      zipAlignEnabled false
      shrinkResources false
      minifyEnabled false
      signingConfig signingConfigs.debug
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      buglyAppId = buglyAppIdDebug
      buglyAppKey = buglyAppKeyDebug
      resValue "string", "bugly_app_id", buglyAppIdDebug
    }
    release {
      debuggable false
      zipAlignEnabled true
      shrinkResources true
      minifyEnabled true
      signingConfig signingConfigs.release
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      buglyAppId = buglyAppIdRelease
      buglyAppKey = buglyAppKeyRelease
      resValue "string", "bugly_app_id", buglyAppIdRelease
    }
  }
}

android.applicationVariants.all { variant ->
  variant.outputs.all { output ->
    //名称
    String appName = ""
    String path = getProjectDir().getPath() + "/src/main/res/values/strings.xml"
    new XmlSlurper().parse(path)."string".find { name ->
      if (name."@name" == "app_name") {
        appName = name
        return true
      }
    }
    //正式版还是测试版
    String typeName = buildType.name
    typeName = typeName.substring(0, 1).toUpperCase() + typeName.substring(1).toLowerCase()
    //build名称
    String buildVer = String.valueOf(rootProject.ext.android.versionCode)
    String buildName = "_build" + buildVer
    //编译日期
    String buildTime = "_" + String.valueOf(rootProject.ext.android.buildTime)
    //打包的电脑
    String computer = "_" + String.valueOf(rootProject.ext.android.computer)
    //后缀名
    String suffix = ".apk"
    //生成输出文件名称
    String fileName = "${appName}${typeName}${buildName}${buildTime}${computer}${suffix}"
    //是否需要删除旧文件APK
    boolean needDelOldApk = false
    //输入文件设置
    if (needDelOldApk) {
      outputFileName = fileName
    } else {
      assemble.doLast {
        //解决Android Studio3.6删除安卓包的问题
        String buildEndTime = "_${new Date().format("yyyyMMdd_HHmm")}"
        fileName = "${appName}${typeName}${buildName}${buildEndTime}${computer}${suffix}"
        project.copy {
          from("${output.outputFile}")
          into("${output.outputFile.parent}")
          rename("${output.outputFile.name}", "${fileName}")
        }
      }
    }
  }
}

//强制使用lib版本，解决个版本导致包重复无法混淆的问题 https://www.jianshu.com/p/c602e6c493ce
//configurations.all {
//  resolutionStrategy {
//    //https://maven.aliyun.com/mvn/search
//    force 'androidx.fragment:fragment:1.3.0-alpha05'
//    force 'com.google.code.gson:gson:2.8.6'
//    force 'com.squareup.okhttp3:okhttp:4.7.2'
//  }
//}

bugly {
  debug = true
  appId = buglyAppId
  appKey = buglyAppKey
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation project(':ablibrary')
  implementation project(':silicompressor')

  //表情 https://github.com/vanniktech/Emoji
  implementation 'com.vanniktech:emoji-ios:0.6.0'
  //封面获取，视频压缩 https://github.com/microshow/RxFFmpeg
  //implementation 'com.github.microshow:RxFFmpeg:4.2.0-lite'

  //必选 https://github.com/dueeeke/DKVideoPlayer/wiki
  implementation 'com.github.dueeeke.dkplayer:dkplayer-java:3.2.6'
  //可选，包含StandardVideoController的实现
  implementation 'com.github.dueeeke.dkplayer:dkplayer-ui:3.2.6'
  //可选，使用exoplayer进行解码（推荐）
  implementation 'com.github.dueeeke.dkplayer:player-exo:3.2.6'

  //弹幕 https://github.com/bilibili/DanmakuFlameMaster
  implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
  implementation 'com.github.ctiao:ndkbitmap-armv7a:0.9.21'

  //SideBar https://github.com/D10NGYANG/DL10SideBar
  implementation 'com.github.D10NGYANG:DL10SideBar:1.0.0'

  //bugly https://bugly.qq.com/docs/user-guide/instruction-manual-android/?v=20190916214340
  implementation 'com.tencent.bugly:crashreport:3.2.32'
  implementation 'com.tencent.bugly:nativecrashreport:3.7.3'

  //中文转拼音 https://github.com/promeG/TinyPinyin
  implementation 'com.github.promeg:tinypinyin:2.0.3' // TinyPinyin核心包，约80KB
  implementation 'com.github.promeg:tinypinyin-lexicons-android-cncity:2.0.3' // 可选，适用于Android的中国地区词典

  //@和#效果 https://github.com/sunhapper/SpEditTool
  implementation 'com.github.sunhapper.SpEditTool:SpEditText:1.0.4'

  //https://github.com/airbnb/epoxy 放在lib中失败
  kapt 'com.airbnb.android:epoxy-processor:4.0.0-beta4'

  //RxHttp https://github.com/liujingxing/okhttp-RxHttp
  implementation 'com.ljx.rxhttp:rxhttp:2.2.4'
  kapt 'com.ljx.rxhttp:rxhttp-compiler:2.2.4' //生成RxHttp类
  //rxandroid在lib中已经存在，就不再次添加了
  implementation 'com.ljx.rxlife2:rxlife-rxjava:2.0.0' //管理RxJava2生命周期，页面销毁，关闭请求

  //日期选择 https://github.com/zyyoona7/WheelPicker
  implementation 'com.github.zyyoona7:wheelview:1.0.8'
  implementation 'com.github.zyyoona7:pickerview:1.1.0'

  //FlexboxLayout https://github.com/google/flexbox-layout
  implementation 'com.google.android:flexbox:2.0.1'
}